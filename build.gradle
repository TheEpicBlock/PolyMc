plugins {
	id 'fabric-loom' version '0.12-SNAPSHOT'
	id 'maven-publish'
	id 'io.github.juuxel.loom-quiltflower' version '1.7.1'
	id 'org.ajoberstar.grgit' version '4.1.0'
}

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

archivesBaseName = project.archives_base_name
version = "${project.mod_version}${getVersionMetadata()}+${project.minecraft_version}"
group = project.maven_group

repositories {
	maven {
		url "https://jitpack.io"
	}
	maven {
		url "https://maven.theepicblock.nl"
		content {
			includeGroup("nl.theepicblock")
		}
	}
	maven {
		url "https://maven.nucleoid.xyz"
		content {
			includeGroup("xyz.nucleoid")
		}
	}
	maven {
		name 'Quilt'
		url 'https://maven.quiltmc.org/repository/release'
		content {
			includeGroupByRegex("org\\.quiltmc.*")
		}
	}
}

dependencies {
	//to change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	modCompileOnly("org.quiltmc.qsl.core:registry:${project.qsl_version}") {
		transitive(false)
	}

	modImplementation include("nl.theepicblock:resource-locator-api:${project.resource_locator_api_version}")
	modImplementation include("xyz.nucleoid:packet-tweaker:${project.packet_tweaker_version}")

	// Compat
	modCompileOnly("com.github.CaffeineMC.lithium-fabric:lithium:${project.lithium_version}") {
		transitive(false)
	}
	modCompileOnly("com.github.iPortalTeam.ImmersivePortalsMod:imm_ptl_core:${project.immersive_portals_version}") {
		transitive(false)
	}
	modCompileOnly("com.github.iPortalTeam.ImmersivePortalsMod:q_misc_util:${project.immersive_portals_version}") {
		transitive(false)
	}
}

sourceSets {
	testmod {
		runtimeClasspath += main.runtimeClasspath + main.output
		compileClasspath += main.compileClasspath + main.output
	}
	datagen {
		runtimeClasspath += main.runtimeClasspath
		compileClasspath += main.compileClasspath
	}
}

loom {
	runs {
		testmodClient {
			client()
			ideConfigGenerated project.rootProject == project
			name = "Test Mod Client"
			source sourceSets.testmod
		}
		testmodServer {
			server()
			ideConfigGenerated project.rootProject == project
			name = "Test Mod Server"
			source sourceSets.testmod
		}
		gametest {
			server()
			name "Game Test"
			vmArg "-Dfabric-api.gametest"
			vmArg "-Dfabric-api.gametest.report-file=${project.buildDir}/junit.xml"
			runDir "build/gametest"
			source sourceSets.testmod
		}
		datagen {
			server()
			ideConfigGenerated false
			source sourceSets.datagen
		}
	}

	accessWidenerPath = file("src/main/resources/polymc.accesswidener")
}

task testmodJar(type: Jar) {
	archiveClassifier = "testmod-dev"
	from sourceSets.testmod.output
}

task remapTestmodJar(type: net.fabricmc.loom.task.RemapJarTask, dependsOn: testmodJar) {
	archiveClassifier = "testmod"
	input = testmodJar.archiveFile.get().asFile
	addNestedDependencies = false
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

// This includes the resources in build/polymc-datagen/<version>/ into the jar
def datagenDir = "$rootProject.buildDir/polymc-datagen/$project.minecraft_version"
sourceSets.main.resources.srcDirs += datagenDir

runDatagen {
	environment("output-dir", datagenDir)
}

build.doLast {
	if (!file(datagenDir).exists()) {
		logger.error("!! Datagen hasn't been run. Vanilla ids aren't included in the build")
	}
}

task getClientSha1 {
	doLast {
		def loomExt = net.fabricmc.loom.LoomGradleExtension.get(project)
		def mcProvider = loomExt.getMinecraftProvider()
		println(mcProvider.getVersionInfo().download("client").sha1())
	}
}

def getVersionMetadata() {
	if (grgit != null) {
		def head = grgit.head()
		def headTag = grgit.tag.list().find {
			it.commit == head
		}

		if (headTag) {
			return "" // This is a release
		}

		def id = head.abbreviatedId

		// Flag the build if the build tree is not clean
		if (!grgit.status().clean) {
			id += "-dirty"
		}

		return "-rev.${id}"
	}
	return "";
}

// Ensure that the encoding is set to UTF-8, no matter what the system default is
// This fixes some edge cases with special characters not displaying correctly
// See http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"

	it.options.release = 16
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	repositories {
		maven {
			name = "teb"
			credentials(PasswordCredentials)
			url = "https://maven.theepicblock.nl"
		}
	}
}
